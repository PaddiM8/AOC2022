fn parse(lines) {
    let stacks = []
    let end = for (line, lineIndex) in lines | withIndex {
        break lineIndex if (line | str::startsWith(" 1"))

        let crates = line
            | str::chunks 4
            | select => x: x[1]
        for (crate, stackIndex) in crates | withIndex {
            stacks | push([]) if len(stacks) <= stackIndex
            stacks[stackIndex] | insert(0, crate) if crate != " "
        }
    }

    parseInstructions(lines, end + 2, stacks)
}

fn parseInstructions(lines, startIndex, stacks) {
    for line in lines[startIndex..] {
        let (count, fromStack, toStack) = '\d+' | regex::findAll(line) | int!
        moveCrates(count, stacks[fromStack - 1], stacks[toStack - 1])
    }

    stacks
        | where => x: len(x) > 0
        | last!
        | join
}

fn moveCrates(count, fromStack, toStack) {
    let index = len(fromStack) - count
    let crates = fromStack[index..]
    fromStack | removeRange(index..)
    toStack | list::extend(crates)
}

cat(argv[1]) | lines | parse | println
